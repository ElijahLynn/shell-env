#!/usr/bin/env perl

use strict;
use warnings;

use Date::Manip 'Delta_Format';
use File::Basename;
use Getopt::Long;
my %opts;
GetOptions(\%opts, 'human|h', 'help') or usage();

use constant NEED_ON_SINCE => 1;
use constant NEED_IDLE     => 2;

sub usage {
  my $me = basename $0;
  die <<EOUSAGE;
EOUSAGE
}

my $who = shift || scalar(getpwuid $<);

open(FINGER, "finger $who|")
  or die "open(finger $who|) failed: $!\n";

my $least_idle_secs;

# The format of finger(1)'s output is such that an idle time line
# always follows an 'On since' line.  If the expected idle time line
# is absent, it indicates that the corresponding session is not idle
# at all.
#
# We aim to find the session with the *least* idle time.
my $state = NEED_ON_SINCE;
while (<FINGER>) {
  if ($state == NEED_ON_SINCE) {
    $state = NEED_IDLE if /\bon since\b/i;
    next;
  }

  if ($state == NEED_IDLE) {
    if (s/\bidle(\s+time)?\b//i) {
      my $secs = parse_idle_time($_);
      if (! defined $least_idle_secs) {
        $least_idle_secs = $secs;
        debug("Least idle now $secs seconds\n");
      }
      elsif ($secs < $least_idle_secs) {
        debug("Found smaller idle time of $secs seconds\n");
        $least_idle_secs = $secs;
      }
      $state = NEED_ON_SINCE;
      next;
    }
    
    debug("Got:\n$_\ninstead of idle line; assuming not idle at all.\n");
    $least_idle_secs = 0;
    last;
  }
}

if ($state == NEED_IDLE) {
  debug("Finished expecting an idle line; assuming not idle at all.\n");
  $least_idle_secs = 0;
}

sub parse_idle_time {
  my ($text) = @_;
  $_ = $text;

  chomp;
  s/^\s+//;
  
  my $secs;
  $secs += $1 * 3600 * 24 if s/(\d+) days?\s+//i;
  $secs += $1 * 3600      if s/(\d+) hours?\s+//i;
  $secs += $1 *   60      if s/(\d+) min(ute)?s?\s+//i;
  $secs += $1             if s/(\d+) sec(ond)?s?\s+//i;

  return $secs;
}

close(FINGER);

if (! $least_idle_secs) {
  # Didn't get any idle time; not logged on.
  exit 1;
}

if ($opts{human}) {
  my @units = Delta_Format(
    $least_idle_secs . 'S',
    0,
    map "%${_}v", qw{M d h m s},
  );
  
  my $i = 0;
  my @out;
  for my $unit (qw{month day hour minute second}) {
    if (my $val = $units[$i]) {
      my $out = "$val $unit";
      $out   .= "s" if $val != 1;
      push @out, $out;
    }
    $i++;
  }
    
  print "@out\n";
}
else {
  print "$least_idle_secs\n";
}

sub debug {
#  warn @_;
}
